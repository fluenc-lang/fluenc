import i32 puts(string str);

import i32 SDL_Init(u32 flags);
import i32 SDL_SetRenderDrawColor(i64 renderer, i32 r, i32 g, i32 b, i32 a);
import i32 SDL_RenderClear(i64 renderer);
import i32 SDL_RenderFillRect(i64 renderer, SDL_Rect rect);
import i32 SDL_PollKeyEvent(SDL_KeyboardEvent key);

import i64 SDL_CreateRenderer(i64 window, i32 index, i32 flags);
import i64 SDL_CreateWindow(string title, i32 x, i32 y, i32 w, i32 h, i32 flags);

import void SDL_RenderPresent(i64 renderer);
import void SDL_PumpEvents();

import void printnum(i32 num);

global SDL_INIT_VIDEO: 0x00000020u32;
global SDL_WINDOWPOS_UNDEFINED: 0x1FFF0000u32;
global SDL_KEYDOWN: 0x300u32;
global SDL_KEYUP: 0x301u32;
global SDLK_a: 0x61u32;
global SDLK_d: 0x64u32;
global SDLK_s: 0x73u32;
global SDLK_w: 0x77u32;

struct SDL_Rect
{
    x: 0,
    y: 0,
    width: 0,
    height: 0
};

struct SDL_KeyboardEvent
{
    type: 0,
    timestamp: 0,
    windowID: 0,
    padding: 0,
    scancode: 0,
    sym: 0u32,
    padding: 0i64
};

struct Player
{
    x: 0,
    y: 0
};

function createKeyboardEvent()
{
    return SDL_KeyboardEvent {};
}

function createMouseMotionEvent()
{
    return SDL_MouseMotionEvent {};
}

function createPlayer()
{
    return Player {};
}

function processKey(SDL_KeyboardEvent key)
{
    SDL_PumpEvents();

    if (SDL_PollKeyEvent(key) == 1)
    {
        return key;
    }

    return nothing;
}

function updatePlayerPosition(Player player, SDL_KeyboardEvent key)
{
    if (key.sym == SDLK_a)
    {
        return player with
        {
            x: player.x - 1
        };
    }

    if (key.sym == SDLK_d)
    {
        return player with
        {
            x: player.x + 1
        };
    }

    if (key.sym == SDLK_w)
    {
        return player with
        {
            y: player.y - 1
        };
    }

    if (key.sym == SDLK_s)
    {
        return player with
        {
            y: player.y + 1
        };
    }

    return player;
}

function updatePlayerPosition(Player player, without key)
{
    return player;
}

function processEvents(Player player)
{
    return updatePlayerPosition(player
        , processKey(
            createKeyboardEvent()
            )
        );
}

function rectFromPlayer(Player player)
{
    return SDL_Rect
    {
        x: player.x,
        y: player.y,
        width: 32,
        height: 32
    };
}

function mainLoop(i64 renderer, Player player)
{
    SDL_SetRenderDrawColor(renderer, 255, 255, 255, 255);

    SDL_RenderClear(renderer);

	SDL_SetRenderDrawColor(renderer, 255, 0, 0, 255);

    SDL_RenderFillRect(renderer
        , rectFromPlayer(player)
        );

    SDL_RenderPresent(renderer);

    return tail mainLoop(renderer
        , processEvents(player)
        );
}

export i32 main()
{
    SDL_Init(SDL_INIT_VIDEO);

    return mainLoop(
        SDL_CreateRenderer(
            SDL_CreateWindow(@"test", 0, 0, 800, 600, 0), 0, 0)
            , createPlayer()
        );
}
