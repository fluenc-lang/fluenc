import i32 puts(string str);
import void printnum(i32 num);

struct Color
{
    r: 0,
    g: 0,
    b: 0
}

struct State
{
    player,
    cells
};

struct Square
{
    x: 0,
    y: 0,
    owner: 0
}

struct EventState
{
    SDL::KeyboardEvent key,
    SDL::MouseMotionEvent motion,
    SDL::MouseButtonEvent button
};

function createEventState()
{
    return EventState
    {
        key: SDL::KeyboardEvent {},
        motion: SDL::MouseMotionEvent {},
        button: SDL::MouseButtonEvent {}
    };
}

function rgb(i32 r, i32 g, i32 b)
{
    return Color
    {
        r: r,
        g: g,
        b: b
    };
}

function createSquare(i32 index, i32 owner)
{
    let x = index % 3;

    return Square
    {
        x: x,
        y: (index - x) / 3,
        owner: owner
    };
}

function square((i32 owner, ...values), i32 index)
{
    return createSquare(index, owner) -> square(...values, index + 1);
}

function square(i32 owner, i32 index)
{
    return createSquare(index, owner);
}

function application(State state)
{
    return square(...state.cells, 0);
}

function bounds(Square square)
{
    let width = (800 - 20) / 3;
    let height = (600 - 20) / 3;

    return SDL::Rect
    {
        x: square.x * (width + 10),
        y: square.y * (height + 10),
        width: width,
        height: height
    };
}

function setRenderDrawColor(i64 renderer, Color color)
{
    return SDL::SDL_SetRenderDrawColor(renderer, color.r, color.g, color.b, 255);
}

function setRenderDrawColor(i64 renderer, without color)
{
    return SDL::SDL_SetRenderDrawColor(renderer, 0, 0, 255, 0);
}

function drawSquare(i64 renderer, Square square)
{
    let colors = [rgb(255, 0, 0), rgb(0, 255, 0)];

    let color = Array::elementAt(...colors, square.owner, 0);

    setRenderDrawColor(renderer, color);

    SDL::SDL_RenderFillRect(renderer, bounds(square));

    return nothing;
}

function draw(i64 renderer, (Square square, ...next))
{
    drawSquare(renderer, square);

    return tail draw(renderer, ...next);
}

function draw(i64 renderer, Square square)
{
    return drawSquare(renderer, square);
}

function containsMouse(SDL::Rect bounds, SDL::MouseMotionEvent motion)
{
    return (motion.x > bounds.x)
        && (motion.x < (bounds.x + bounds.width))
        && (motion.y > bounds.y)
        && (motion.y < (bounds.y + bounds.height));
}

function isPressed(SDL::MouseButtonEvent button)
{
    return button.type == SDL::MOUSEBUTTONDOWN;
}

function updateCell(State state, EventState events, i32 index, Square square)
{
    if (square.owner >= 0)
    {
        return state;
    }

    if (containsMouse(bounds(square), events.motion) && isPressed(events.button))
    {
        return state with
        {
            player: (state.player + 1) % 2,
            cells: Array::set(...state.cells, index, state.player, 0)
        };
    }

    return state;
}

function update(State state, EventState events, i32 index, (Square square, ...squares))
{
    let updatedState = updateCell(state, events, index, square);

    return tail update(updatedState, events, index + 1, ...squares);
}

function update(State state, EventState events, i32 index, Square square)
{
    return updateCell(state, events, index, square);
}

function mainLoop(i64 renderer, State state)
{
    SDL::SDL_SetRenderDrawColor(renderer, 0, 0, 0, 255);
    SDL::SDL_RenderClear(renderer);

    let ui = application(state);

    draw(renderer, ...ui);

    SDL::SDL_RenderPresent(renderer);

    let events = SDL::SDL_WaitEventEx(createEventState());

    return tail mainLoop(renderer
        , update(state, events, 0, ...ui)
        );
}

function initState()
{
    return State
    {
        player: 0,
        cells: [-1, -1, -1, -1, -1, -1, -1, -1, -1]
    };
}

export i32 main()
{
    SDL::SDL_Init(SDL::INIT_VIDEO);

    let window = SDL::SDL_CreateWindow(@"test", 0, 0, 800, 600, 0);
    let renderer = SDL::SDL_CreateRenderer(window, 0, 0);

    return mainLoop(renderer
        , initState()
        );
}
